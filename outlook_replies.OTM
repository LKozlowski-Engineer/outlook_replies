'Sources
'http://stackoverflow.com/questions/28840066/forward-email-with-its-attachment-in-outlook-2010
'http://www.ozgrid.com/VBA/ExtractLastWord.htm
'https://www.mrexcel.com/forum/excel-questions/234028-visual-basic-applications-count-substrings-string.html
'https://msdn.microsoft.com/en-us/library/office/ff868695.aspx

Sub forwardEmails()
    'Creates the names of the new and old emails
    Dim item As MailItem
    Dim curMail As MailItem
    
    'Creates a string variable to store the requestor's name in
    Dim requestorName As String
    
    'Prepares the variables needed to extract all the recipients in the email and reattach them to another email
    Dim recip As Recipient
    Dim pa As PropertyAccessor
    Const PR_SMTP_ADDRESS As String = _
        "http://schemas.microsoft.com/mapi/proptag/0x39FE001E"
    Dim recipStr As String
    
    'Selects the old email
    Set item = Application.ActiveExplorer.Selection.item(1)
    
    'Creates a new email as the forward of the old email
    'Email is being forwarded so no need to reattach items
    Set curMail = item.Forward
    
    'Ensures that the proper email is being
    curMail.SentOnBehalfOfName = "leon.kozlowski@wescoair.com"
    
    'Adds the old sender's email address as the receiver of the new email
    curMail.To = item.SenderEmailAddress
    
    'Get all of the recipients of the old email and puts them in the CC field of the new email
    For Each recip In item.Recipients
        Set pa = recip.PropertyAccessor
        recipStr = pa.GetProperty(PR_SMTP_ADDRESS) & "; " & recipStr
    Next
    curMail.CC = recipStr
    
    'Gets the requestor's name from the old email. Name will be used to format the bod of the new email
    requestorName = item.SenderName
    'If there is more than one (1) space/two (2) name in the email, remove the last name
    If Len(requestorName) - Len(Replace(requestorName, " ", "")) > 1 Then
        requestorName = Left(requestorName, InStrRev(requestorName, " "))
    'If there is no spaces/one (1) name in the email, give a notification stating the circumstance
    ElseIf Len(requestorName) - Len(Replace(requestorName, " ", "")) = 0 Then
        MsgBox "Chances this is a department." & vbNewLine & vbNewLine _
            & "Make sure to check the name before sending the email."
    'Removes the last name and leaves only the first name
    Else
        requestorName = Left(requestorName, InStr(requestorName, " "))
    End If
    
    'Gets rid of white space
    requestorName = Trim(requestorName)
    
    If requestorName = "Tooling" Then
        requestorName = "Laura"
    End If

    'Creates the body of the email along with the signature provided by Wesco
    curMail.HTMLBody = "Hello " & requestorName & ",<br/><br/>Your request has been completed! =)" & curMail.HTMLBody
    
    curMail.Display
    'curMail.Send
End Sub
